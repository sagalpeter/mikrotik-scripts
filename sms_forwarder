# SMS forwarding script for MikroTIK
# Use with scheduler with preferred interval
# Tested on RBD53GR-5HacD2HnD&R11e-LTE6 (hAP acÂ³ LTE6 kit), RouterOS 7.14

# e-mail address of receiver
:local emailTo "some@email.example"
# filename for sms backups (appending every sms to this file)
:local smsBackupFile "sms-backup.txt"
# other variables
:local emailMsg
:local fileContents

# iterate through sms inbox
:foreach i in=[/tool sms inbox find] do={
  # compose message to be sent
  :set emailMsg "Phone: $[/tool sms inbox get $i phone]\nTime: $[/tool sms inbox get $i timestamp]\nMessage: $[/tool sms inbox get $i message]";

  # log
  :log info "New SMS Received:: Phone: $[/tool sms inbox get $i phone] Time: $[/tool sms inbox get $i timestamp] Message: $[/tool sms inbox get $i message]";

  # send message to email
  /tool e-mail send to=$emailTo subject="New SMS received" body=$emailMsg;

  # check if backup file exists, if not create new one
  :if ([:len [/file find name=$smsBackupFile]] = 0) do={
    /file print file=$smsBackupFile;
    /file set $smsBackupFile contents="#SMS\n";
  }

  # backup this message into file
  :set fileContents [/file get $smsBackupFile contents];
  :set fileContents ($fileContents . "\n\n" . $emailMsg);
  /file set $smsBackupFile contents=$fileContents;

  # remove this message, so it will not be sent to e-mail again
  /tool sms inbox remove $i;
}
